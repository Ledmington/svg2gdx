plugins {
    id 'application'
}

// utility function
static def path(final String ...s) {
    return String.join(File.separator, s)
}

String version = "0.0.0"
String fatJarBaseName = "${appNameLowerCase}-cli-${version}"
String fatJarName = "${fatJarBaseName}.jar"
String fatJarPath = path("${project.layout.buildDirectory.get().toString()}", "libs", "${fatJarName}")
String mainClassName = "${basePackage}.svg2gdx.Main"

final String gdxVersion = '1.12.1'
dependencies {
    implementation project(':lib')

    // libGDX
    implementation "com.badlogicgames.gdx:gdx:${gdxVersion}"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:${gdxVersion}"
    runtimeOnly "com.badlogicgames.gdx:gdx-platform:${gdxVersion}:natives-desktop"

    // object sizes at runtime
    implementation 'org.openjdk.jol:jol-core:0.17'
}

application {
    mainClass = mainClassName
}

run {
    args = ["--test", "../logo.svg"]
    jvmArgs = []
}

tasks.register('fatJar', Jar) {
    group 'distribution'
    description 'Builds the fat jar of the application'
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        archiveBaseName = appNameLowerCase
        attributes(
                // "Implementation-Title": "Gradle",
                "Implementation-Version": "${version}",
                "Main-Class": "$mainClassName"
        )
    }
    archiveBaseName = "${fatJarBaseName}"

    doLast {
        printf "The fat jar is %s\n", fatJarPath
        printf "Fat jar size: %.3f MB\n", new File(fatJarPath).length() / 1_000_000L
    }
}

tasks.build.dependsOn(fatJar)
tasks.javadoc.enabled = false
